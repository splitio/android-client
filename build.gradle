buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.0'
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.31.0"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'kotlin-android'
apply plugin: 'com.vanniktech.maven.publish'
apply from: 'spec.gradle'
apply from: 'jacoco.gradle'

ext {
    splitVersion = '5.3.1-rc1'
    jacocoVersion = '0.8.8'
}

android {
    compileSdk 33
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {

        minSdk 19
        targetSdk 31
        multiDexEnabled true

        consumerProguardFiles 'split-proguard-rules.pro'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }

    configurations {
        javadocDeps
    }


    testOptions {
        unitTests.returnDefaultValues = true
        
        // Configure JaCoCo for all test tasks
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDirs += 'src/test/kotlin'
            java.srcDirs += sharedTestDir
        }
        androidTest {
            java.srcDirs += sharedTestDir
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "SPLIT_VERSION_NAME", "\"${splitVersion}\"")
            buildConfigField("String", "FLAGS_SPEC", "\"${flagsSpec}\"")
        }
        release {
            buildConfigField("String", "SPLIT_VERSION_NAME", "\"${splitVersion}\"")
            buildConfigField("String", "FLAGS_SPEC", "\"${flagsSpec}\"")
        }
    }
    namespace 'io.split.android.android_client'
    lint {
        abortOnError false
        disable 'InvalidPackage'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {

    def roomVersion = '2.4.3'
    def workVersion = '2.7.1'
    def lifecycleVersion = '2.5.1'
    def annotationVersion = '1.2.0'
    def gsonVersion = '2.10.1'
    def guavaVersion = '32.1.3-android'
    def snakeYamlVersion = '2.2'
    def playServicesVersion = '18.2.0'
    def multidexVersion = '2.0.1'

    def jUnitVersion = '4.13.2'
    def mockitoVersion = '4.8.0'
    def hamcrestVersion = '1.3'
    def apacheCommonsVersion = '3.12.0'
    def kotlinVer = '1.5.31'
    def mockWebServerVersion = '4.12.0'

    def testRulesVersion = '1.4.0'
    def jUnitExtVersion = '1.1.3'
    def testRunnerVersion = '1.5.1'
    def orchestratorVersion = '1.4.2'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"

    implementation "androidx.work:work-runtime:$workVersion"

    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"

    implementation "androidx.annotation:annotation:$annotationVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "org.yaml:snakeyaml:$snakeYamlVersion"
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"
    implementation "androidx.multidex:multidex:$multidexVersion"

    // Test
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testImplementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"
    testImplementation "com.squareup.okhttp3:okhttp-tls:$mockWebServerVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVer"
    testImplementation "com.google.guava:guava:$guavaVersion"

    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.ext:junit:$jUnitExtVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestUtil "androidx.test:orchestrator:$orchestratorVersion"

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"
    androidTestImplementation "com.squareup.okhttp3:okhttp-tls:$mockWebServerVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "androidx.work:work-testing:$workVersion"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVer"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVer"
    androidTestImplementation "com.google.guava:guava:$guavaVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
}

def splitPOM = {
    name = 'Split Android SDK'
    description = 'Official Split Android SDK'
    url = 'https://github.com/splitio/android-client'

    licenses {
        license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id = 'sdks'
            email = 'sdks@split.io'
        }
    }

    scm {
        connection = 'scm:git:git@github.com:splitio/android-client.git'
        developerConnection = 'scm:git@github.com:splitio/android-client.git'
        url = 'https://github.com/splitio/android-client'
    }
}

def devRepo = {
    name = "DevelopmentRepo"
    url = 'https://splitio.jfrog.io/artifactory/maven-all-virtual'
    credentials {
        username = System.getenv('ARTIFACTORY_USER')
        password = System.getenv('ARTIFACTORY_TOKEN')
    }
}

mavenPublishing {
  coordinates("io.split.client", "android-client", splitVersion)
  signAllPublications()
  pom(splitPOM)
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    archiveClassifier = 'javadoc'
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task printReleaseDependenciesToFile {
    doLast {
        def outputFile = new File("$projectDir/deps.txt")
        def processBuilder = new ProcessBuilder("./gradlew", ":android-client:dependencies", "--configuration", "releaseRuntimeClasspath", "-q")

        outputFile.parentFile.mkdirs()
        outputFile.text = '' // Clear the file before writing
        processBuilder.redirectOutput(outputFile)
        processBuilder.directory(project.rootDir)

        try {
            Process process = processBuilder.start()
            process.waitFor()

            def filteredLines = outputFile.readLines().findAll { !it.contains("BUILD SUCCESSFUL") }

            // Write the filtered content back to the file
            outputFile.withPrintWriter { writer ->
                filteredLines.each(writer.&println)
            }
        } catch (Exception e) {
            e.printStackTrace()
        }
    }
}

preBuild.dependsOn printReleaseDependenciesToFile

tasks.withType(Test) {
    systemProperties['junit.jupiter.execution.parallel.enabled'] = true
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 100
    maxHeapSize = "1024m"
}
