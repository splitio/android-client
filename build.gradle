buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
    }
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

ext {
    splitVersion = '2.9.1-rc3'
}

android {
    compileSdkVersion 30
    targetCompatibility = '1.8'
    sourceCompatibility = '1.8'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {

        minSdkVersion 15
        targetSdkVersion 29
        versionCode 0
        versionName splitVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'split-proguard-rules.pro'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    configurations {
        javadocDeps
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }
        test {
            java.srcDirs += "src/sharedTest/java"
        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "SPLIT_VERSION_NAME", "\"${splitVersion}\"")
        }
        release {
            buildConfigField("String", "SPLIT_VERSION_NAME", "\"${splitVersion}\"")
        }
    }
}

dependencies {

    def roomVersion = '2.3.0'
    def workVersion = '2.6.0'
    def lifecycleVersion = '2.3.1'
    def annotationVersion = '1.2.0'
    def gsonVersion = '2.8.9'
    def guavaVersion = '31.0.1-android'
    def snakeYamlVersion = '1.29'
    def jetBrainsAnnotationsVersion = '22.0.0'
    def okHttpVersion = '3.12.0'
    def playServicesVersion = '17.6.0'

    def jUnitVersion = '4.13.2'
    def mockitoVersion = '3.12.4'
    def hamcrestVersion = '1.3'
    def apacheCommonsVersion = '3.12.0'
    def mockWebServerVersion = '3.12.0'

    def androidTestVersion = '1.4.0'

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "androidx.room:room-runtime:$roomVersion"
    annotationProcessor "androidx.room:room-compiler:$roomVersion"

    implementation "androidx.work:work-runtime:$workVersion"

    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"

    implementation "androidx.annotation:annotation:$annotationVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.google.guava:guava:$guavaVersion"

    implementation "org.yaml:snakeyaml:$snakeYamlVersion"
    implementation "org.jetbrains:annotations:$jetBrainsAnnotationsVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.google.android.gms:play-services-base:$playServicesVersion"

    // Test
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    testImplementation "org.apache.commons:commons-lang3:$apacheCommonsVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"

    androidTestImplementation "androidx.test:core:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:$mockWebServerVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"
    androidTestImplementation "androidx.work:work-testing:$workVersion"
}

afterEvaluate {
    publishing {
        publications {
            project.components.all { print("PRJ NAME: " + name) }
            release(MavenPublication) {
                from components.release

                groupId = 'io.split.client'
                version = splitVersion
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'Split Android SDK'
                    packaging = 'aar'
                    description = 'Official Split Android SDK'
                    url = 'https://github.com/splitio/android-client'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'sarrubia'
                            name = 'Sebastian Arrubia'
                            email = 'sebastian@split.io'
                        }

                        developer {
                            id = 'fernando'
                            name = 'Fernando Martin'
                            email = 'fernando@split.io'
                        }
                    }

                    scm {
                        connection = 'scm:git:git@github.com:splitio/android-client.git'
                        developerConnection = 'scm:git@github.com:splitio/android-client.git'
                        url = 'https://github.com/splitio/android-client'
                    }
                }

                repositories {
                    maven {
                        def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                            username = ossrhUsername
                            password = ossrhPassword
                        }
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
//    archiveClassifier = "sources"
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
//    archiveClassifier = 'javadoc'
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
